
src = 'main.cc synth.cc mainwindow.cc synthview.cc patchview.cc sectionview.cc moduleview.cc'

srcfiles = [('src/' + x) for x in Split(src)]

nmedit = Environment(CPPPATH = ['../libs/libppf', '../libs/libpdl',
                                '../libs/libnmpatch', '../libs/libnmprotocol'],
                     LIBPATH = ['/lib', '/usr/lib', '/usr/local/lib',
                                '../libs/libppf', '../libs/libpdl',
                                '../libs/libnmpatch', '../libs/libnmprotocol'],
                     LIBS = ['nmprotocol', 'nmpatch', 'pdl', 'ppf'])

opts = Options('nmedit.conf')
opts.Add(PathOption('PREFIX',
                    'Install dir (Defult: /usr/local)',
                    '/usr/local')) 
opts.Update(nmedit) 
opts.Save('nmedit.conf', nmedit)

tcllibs = Split('tcl tcl8.5 tcl8.4 tcl8.3 tcl8.2 tcl8.1 tcl8.0')

conf = Configure(nmedit)

conf.env.Append(CPPFLAGS='-DVERSION=\'"0.1"\'')

if not conf.CheckLib('ppf'):
   print "libppf missing!"
   Exit(1);

if not conf.CheckLib('pdl'):
   print "libpdl missing!"
   Exit(1);

if not conf.CheckLib('nmpatch'):
   print "libnmpatch missing!"
   Exit(1);

if not conf.CheckLib('nmprotocol'):
   print "libnmprotocol missing!"
   Exit(1);

tclfound = False
for tcllib in tcllibs:
   if conf.CheckLib(tcllib):
      conf.env.Append(LIBS=[tcllib])
      tclfound = True
      break
if not tclfound:
   print 'libtcl missing!'
   Exit(1);

if not conf.CheckLib('fltk'):
   print 'libfltk missing!'
   Exit(1);
conf.env.ParseConfig('fltk-config --cxxflags --ldflags')

nmedit = conf.Finish()

idir_prefix = '$PREFIX'
idir_bin    = '$PREFIX/bin'

prog = nmedit.Program('nmedit', srcfiles)

nmedit.Install(idir_bin, prog)
nmedit.Alias('install', idir_prefix)

